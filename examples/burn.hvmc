// Decreases a tree of Î»-encoded binary counters until they're all 0 (parallel).
// Takes about ~16s on Apple M1, and ~0.5s on RTX 4090

@c4  = ([[[(d c) (c b)] (b a)] (a R)] (d R))
@c6  = ([[[[[(f e) (e d)] (d c)] (c b)] (b a)] (a R)] (f R))
@c8  = ([[[[[[[(h g) (g f)] (f e)] (e d)] (d c)] (c b)] (b a)] (a R)] (h R))
@c10 = ([[[[[[[[[(j i) (i h)] (h g)] (g f)] (f e)] (e d)] (d c)] (c b)] (b a)] (a R)] (j R))
@c12 = ([[[[[[[[[[[(l k) (k j)] (j i)] (i h)] (h g)] (g f)] (f e)] (e d)] (d c)] (c b)] (b a)] (a R)] (l R))
@c14 = ([[[[[[[[[[[[[(n m) (m l)] (l k)] (k j)] (j i)] (i h)] (h g)] (g f)] (f e)] (e d)] (d c)] (c b)] (b a)] (a R)] (n R))
@c16 = ([[[[[[[[[[[[[[[(p o) (o n)] (n m)] (m l)] (l k)] (k j)] (j i)] (i h)] (h g)] (g f)] (f e)] (e d)] (d c)] (c b)] (b a)] (a R)] (p R))

@S = (a ((a b) (* b)))
@Z = (* (a a))

@O = (xs ((xs r) (* (* r))))
@I = (xs (* ((xs r) (* r))))
@E = (* (* (e e)))

@decO = (p idecp) & @I ~ (decp idecp) & @dec ~ (p decp)
@decI = (p lowp) & @low ~ (p lowp)
@dec  = ((@decO (@decI (@E R))) R)

@lowO = (p oop) & @O ~ (p op) & @O ~ (op oop)
@lowI = (p oip) & @I ~ (p ip) & @O ~ (ip oip)
@low  = ((@lowO (@lowI (@E R))) R)

@runO = (p R) & @run ~ (decop R) & @dec ~ (op decop) & @O ~ (p op)
@runI = (p R) & @run ~ (decip R) & @dec ~ (ip decip) & @I ~ (p ip)
@run  = ((@runO (@runI (@E R))) R)

@brnZ = R & @run ~ (val R) & @c10 ~ (@I (@E val))
@brnS = ([p0 p1] (r0 r1)) & @brn ~ (p0 r0) & @brn ~ (p1 r1)
@brn  = ((@brnS (@brnZ r)) r)

@main
  = R
  & @c12 ~ (@S (@Z dep))
  & @brn ~ (dep R)
